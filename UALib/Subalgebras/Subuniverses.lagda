---
layout: default
title : Subalgebras.Subuniverses module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="subuniverses">Subuniverses</a>

This section presents the [Subalgebras.Subuniverses][] module of the [Agda Universal Algebra Library][].

We start by defining a type that represents the important concept of **subuniverse**. Suppose 𝑨 is an algebra.  A subset B ⊆ ∣ 𝑨 ∣ is said to be **closed under the operations of** 𝑨 if for each 𝑓 ∈ ∣ 𝑆 ∣ and all tuples 𝒃 : ∥ 𝑆 ∥ 𝑓 → 𝐵 the element (𝑓 ̂ 𝑨) 𝒃 belongs to B. If a subset B ⊆ 𝐴 is closed under the operations of 𝑨, then we call B a **subuniverse** of 𝑨.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; 𝓞; 𝓥)
open import MGS-Subsingleton-Theorems using (global-dfunext)

module Subalgebras.Subuniverses {𝑆 : Signature 𝓞 𝓥}{gfe : global-dfunext} where

open import Terms.Operations{𝑆 = 𝑆}{gfe} public
open import Relation.Unary using (⋂) public

\end{code}

We first show how to represent in [Agda][] the collection of subuniverses of an algebra A.  Since a subuniverse is viewed as a subset of the domain of A, we define it as a predicate on ∣ A ∣.  Thus, the collection of subuniverses is a predicate on predicates on ∣ A ∣.

\begin{code}

module _ {𝓤 𝓦 : Universe} where

 Subuniverses : (𝑨 : Algebra 𝓤 𝑆) → Pred (Pred ∣ 𝑨 ∣ 𝓦)(𝓞 ⊔ 𝓥 ⊔ 𝓤 ⊔ 𝓦)
 Subuniverses 𝑨 B = (𝑓 : ∣ 𝑆 ∣)(𝑎 : ∥ 𝑆 ∥ 𝑓 → ∣ 𝑨 ∣) → Im 𝑎 ⊆ B → (𝑓 ̂ 𝑨) 𝑎 ∈ B

\end{code}

Here's one way to construct an algebra out of a subuniverse.

\begin{code}

 SubunivAlg : (𝑨 : Algebra 𝓤 𝑆)(B : Pred ∣ 𝑨 ∣ 𝓦) → B ∈ Subuniverses 𝑨 → Algebra (𝓤 ⊔ 𝓦) 𝑆
 SubunivAlg 𝑨 B B∈SubA = Σ B , λ 𝑓 𝑏 → (𝑓 ̂ 𝑨)(fst ∘ 𝑏) , B∈SubA 𝑓 (fst ∘ 𝑏)(snd ∘ 𝑏)

\end{code}



#### <a id="subuniverses-as-records">Subuniverses as records</a>

We could define the type of subuniverses as a record as follows.

\begin{code}

 record Subuniverse {𝑨 : Algebra 𝓤 𝑆} : ov (𝓤 ⊔ 𝓦) ̇ where
  constructor mksub
  field
    sset  : Pred ∣ 𝑨 ∣ 𝓦
    isSub : sset ∈ Subuniverses 𝑨

\end{code}

For example, we could use such a type to prove that the equalizer of two homomorphisms is a subuniverse.

\begin{code}

 𝑬𝑯-is-subuniverse : {𝑨 : Algebra 𝓤 𝑆}(𝑩 : Algebra 𝓦 𝑆)
                     (g h : hom 𝑨 𝑩) → Subuniverse {𝑨 = 𝑨}

 𝑬𝑯-is-subuniverse 𝑩 g h = mksub (𝑬𝑯 𝑩 g h) λ 𝑓 𝒂 x → 𝑬𝑯-closed 𝑩 g h 𝑓 𝒂 x

\end{code}

#### <a id="subuniverse-generation">Subuniverse Generation</a>

If `𝑨` is an algebra and `X ⊆ ∣ 𝑨 ∣` a subset of the universe of `𝑨`, then the **subuniverse of** `𝑨` **generated by** `X` is typically denoted by `Sg`<sup>`𝑨`</sup>`(X)` and defined to be the smallest subuniverse of `𝑨` containing `X`.  Equivalently,

`Sg`<sup>`𝑨`</sup>`(X)`  =  `⋂` { `U` : `U` is a subuniverse of `𝑨` and  `B ⊆ U` }.

We now define an inductive type `Sg` that represents the subuniverse generated by a given subset elements from the domain of an algebra.

\begin{code}

 data Sg (𝑨 : Algebra 𝓤 𝑆)(X : Pred ∣ 𝑨 ∣ 𝓦) : Pred ∣ 𝑨 ∣ (𝓞 ⊔ 𝓥 ⊔ 𝓦 ⊔ 𝓤) where
  var : ∀ {v} → v ∈ X → v ∈ Sg 𝑨 X
  app : (𝑓 : ∣ 𝑆 ∣)(𝑎 : ∥ 𝑆 ∥ 𝑓 → ∣ 𝑨 ∣) → Im 𝑎 ⊆ Sg 𝑨 X → (𝑓 ̂ 𝑨) 𝑎 ∈ Sg 𝑨 X

\end{code}

Recall, in type theory we represent subsets as predicates, which is why `X ⊆ ∣ 𝑨 ∣` is represented by the type `X : Pred ∣ 𝑨 ∣ 𝓦` in the definition of `Sg`.

For any subset `X` of the domain `∣ 𝑨 ∣` of an algebra `𝑨`, the type `Sg X` does indeed represent a subuniverse of `𝑨`, and proving this using the inductive type `Sg` is trivial.

\begin{code}

module _ {𝓤 𝓦 : Universe} where

 sgIsSub : {𝑨 : Algebra 𝓤 𝑆}{X : Pred ∣ 𝑨 ∣ 𝓦} → Sg 𝑨 X ∈ Subuniverses 𝑨
 sgIsSub = app

\end{code}

Next we prove by structural induction that `Sg X` is the smallest subuniverse of `𝑨` containing `X`.

\begin{code}

 sgIsSmallest : {𝓡 : Universe}(𝑨 : Algebra 𝓤 𝑆){X : Pred ∣ 𝑨 ∣ 𝓦}(Y : Pred ∣ 𝑨 ∣ 𝓡)
  →             Y ∈ Subuniverses 𝑨  →  X ⊆ Y  →  Sg 𝑨 X ⊆ Y

 sgIsSmallest _ _ _ XinY (var Xv) = XinY Xv

 sgIsSmallest 𝑨 Y YsubA XinY (app 𝑓 𝑎 SgXa) = fa∈Y
  where
  IH : Im 𝑎 ⊆ Y
  IH i = sgIsSmallest 𝑨 Y YsubA XinY (SgXa i)

  fa∈Y : (𝑓 ̂ 𝑨) 𝑎 ∈ Y
  fa∈Y = YsubA 𝑓 𝑎 IH

\end{code}

When the element of `Sg X` is constructed as `app 𝑓 𝑎 SgXa`, we may assume (the induction hypothesis) that the arguments in the tuple `𝑎` belong to `Y`. Then the result of applying `𝑓` to `𝑎` also belongs to `Y` since `Y` is a subuniverse.



#### <a id="subuniverse-lemmas">Subuniverse Lemmas</a>

Here we formalize a few basic properties of subuniverses. First, the intersection of subuniverses is again a subuniverse.

\begin{code}

module _ {𝓘 𝓤 𝓦 : Universe} where

 sub-intersection : {𝑨 : Algebra 𝓤 𝑆}{I : 𝓘 ̇}{𝒜 : I → Pred ∣ 𝑨 ∣ 𝓦}
  →                 ((i : I) → 𝒜 i ∈ Subuniverses 𝑨)
                    --------------------------------
  →                 ⋂ I 𝒜 ∈ Subuniverses 𝑨

 sub-intersection AiSub 𝑓 𝑎 ⋂AIma = λ i → AiSub i 𝑓 𝑎 λ j → ⋂AIma j i

\end{code}

Next, subuniverses are closed under the action of term operations.

\begin{code}

module _ {𝓤 𝓦 : Universe} where

 sub-term-closed : {𝓧 : Universe}{X : 𝓧 ̇}(𝑨 : Algebra 𝓤 𝑆){B : Pred ∣ 𝑨 ∣ 𝓦}
  →                (B ∈ Subuniverses 𝑨) → (t : Term X)(b : X → ∣ 𝑨 ∣)
  →                (∀ x → b x ∈ B) → ((t ̇ 𝑨) b) ∈ B

 sub-term-closed 𝑨 AB (ℊ x) b b∈B = b∈B x

 sub-term-closed 𝑨 {B} AB (node 𝑓 𝑡) b b∈B =
  AB 𝑓 (λ z → (𝑡 z ̇ 𝑨) b) λ x → sub-term-closed 𝑨 {B} AB (𝑡 x) b b∈B

\end{code}

Alternatively, we could express the preceeding fact using an inductive type representing images of terms.

\begin{code}

 data TermImage (𝑨 : Algebra 𝓤 𝑆)(Y : Pred ∣ 𝑨 ∣ 𝓦) : Pred ∣ 𝑨 ∣ (𝓞 ⊔ 𝓥 ⊔ 𝓤 ⊔ 𝓦)
  where
  var : ∀ {y : ∣ 𝑨 ∣} → y ∈ Y → y ∈ TermImage 𝑨 Y
  app : ∀ 𝑓 𝑡 → (∀ i → 𝑡 i ∈ TermImage 𝑨 Y) → (𝑓 ̂ 𝑨) 𝑡 ∈ TermImage 𝑨 Y

\end{code}

By what we proved above, it should come as no surprise that `TermImage 𝑨 Y` is a subuniverse of 𝑨 that contains Y.

\begin{code}

 TermImageIsSub : {𝑨 : Algebra 𝓤 𝑆}{Y : Pred ∣ 𝑨 ∣ 𝓦} → TermImage 𝑨 Y ∈ Subuniverses 𝑨
 TermImageIsSub = app

 Y⊆TermImageY : {𝑨 : Algebra 𝓤 𝑆}{Y : Pred ∣ 𝑨 ∣ 𝓦} → Y ⊆ TermImage 𝑨 Y
 Y⊆TermImageY {a} a∈Y = var a∈Y

\end{code}

Since `Sg 𝑨 Y` is the smallest subuniverse containing Y, we obtain the following inclusion.

\begin{code}

 SgY⊆TermImageY : (𝑨 : Algebra 𝓤 𝑆)(Y : Pred ∣ 𝑨 ∣ 𝓦) → Sg 𝑨 Y ⊆ TermImage 𝑨 Y
 SgY⊆TermImageY 𝑨 Y = sgIsSmallest 𝑨 (TermImage 𝑨 Y) TermImageIsSub Y⊆TermImageY

\end{code}




#### <a id="properties-of-homomorphisms">Homomorphic images are subuniverses</a>

Now that we have developed the machinery of subuniverse generation, we can prove two basic facts that play an important role in many theorems about algebraic structures. First, the image of a homomorphism is a subuniverse of its codomain.

\begin{code}

 hom-image-is-sub : {𝑨 : Algebra 𝓤 𝑆}{𝑩 : Algebra 𝓦 𝑆}
                    (ϕ : hom 𝑨 𝑩) → (HomImage 𝑩 ϕ) ∈ Subuniverses 𝑩

 hom-image-is-sub {𝑨}{𝑩} ϕ 𝑓 b Imfb = eq ((𝑓 ̂ 𝑩) b) ((𝑓 ̂ 𝑨) ar) γ
  where
  ar : ∥ 𝑆 ∥ 𝑓 → ∣ 𝑨 ∣
  ar = λ x → Inv ∣ ϕ ∣ (Imfb x)

  ζ : ∣ ϕ ∣ ∘ ar ≡ b
  ζ = gfe (λ x → InvIsInv ∣ ϕ ∣ (Imfb x))

  γ : (𝑓 ̂ 𝑩) b ≡ ∣ ϕ ∣ ((𝑓 ̂ 𝑨) ar)
  γ = (𝑓 ̂ 𝑩) b            ≡⟨ ap (𝑓 ̂ 𝑩)(ζ ⁻¹) ⟩
      (𝑓 ̂ 𝑩) (∣ ϕ ∣ ∘ ar) ≡⟨(∥ ϕ ∥ 𝑓 ar)⁻¹ ⟩
      ∣ ϕ ∣ ((𝑓 ̂ 𝑨) ar)   ∎

\end{code}


Next we prove the important fact that homomorphisms are uniquely determined by their values on a generating set.

\begin{code}

 hom-unique : funext 𝓥 𝓦 → {𝑨 : Algebra 𝓤 𝑆}{𝑩 : Algebra 𝓦 𝑆}
              (X : Pred ∣ 𝑨 ∣ 𝓤)  (g h : hom 𝑨 𝑩)
  →           (∀ (x : ∣ 𝑨 ∣)  →  x ∈ X  →  ∣ g ∣ x ≡ ∣ h ∣ x)
              --------------------------------------------
  →           (∀ (a : ∣ 𝑨 ∣) → a ∈ Sg 𝑨 X → ∣ g ∣ a ≡ ∣ h ∣ a)

 hom-unique _ _ _ _ gx≡hx a (var x) = (gx≡hx) a x

 hom-unique fe {𝑨}{𝑩} X g h gx≡hx a (app 𝑓 𝒂 SgIma) =
  ∣ g ∣ ((𝑓 ̂ 𝑨) 𝒂)     ≡⟨ ∥ g ∥ 𝑓 𝒂 ⟩
  (𝑓 ̂ 𝑩)(∣ g ∣ ∘ 𝒂 )   ≡⟨ ap (𝑓 ̂ 𝑩)(fe IH) ⟩
  (𝑓 ̂ 𝑩)(∣ h ∣ ∘ 𝒂)    ≡⟨ ( ∥ h ∥ 𝑓 𝒂 )⁻¹ ⟩
  ∣ h ∣ ((𝑓 ̂ 𝑨) 𝒂 )    ∎
  where
   IH = λ x → hom-unique fe {𝑨}{𝑩} X g h gx≡hx (𝒂 x) (SgIma x)

\end{code}


---------------------------------

[↑ Subalgebras](Subalgebras.html)
<span style="float:right;">[Subalgebras.Subalgebras →](Subalgebras.Subalgebras.html)</span>


{% include UALib.Links.md %}
