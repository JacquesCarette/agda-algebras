---
layout: default
title : UALib.Subalgebras.Generation module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="subuniverse-generation">Subuniverse generation</a>

This section presents the [UALib.Subalgebras.Generation][] module of the [Agda Universal Algebra Library][].

Here we define the inductive type of the subuniverse generated by a given collection of elements from the domain of an algebra, and prove that what we have defined is indeed the smallest subuniverse containing the given elements.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UALib.Algebras using (Signature; 𝓞; 𝓥; Algebra; _↠_)
open import UALib.Prelude.Preliminaries using (global-dfunext; Universe; _̇)


module UALib.Subalgebras.Generation
 {𝑆 : Signature 𝓞 𝓥}{gfe : global-dfunext}
 {𝕏 : {𝓤 𝓧 : Universe}{X : 𝓧 ̇ }(𝑨 : Algebra 𝓤 𝑆) → X ↠ 𝑨}
 where


open import UALib.Subalgebras.Subuniverses{𝑆 = 𝑆}{gfe}{𝕏} public

\end{code}

We define an inductive type representing the subuniverse generated by a set in the obvious way.

\begin{code}

data Sg {𝓤 𝓦 : Universe}(𝑨 : Algebra 𝓤 𝑆)(X : Pred ∣ 𝑨 ∣ 𝓦) :
 Pred ∣ 𝑨 ∣ (𝓞 ⊔ 𝓥 ⊔ 𝓦 ⊔ 𝓤) where
  var : ∀ {v} → v ∈ X → v ∈ Sg 𝑨 X
  app : (f : ∣ 𝑆 ∣)(𝒂 : ∥ 𝑆 ∥ f → ∣ 𝑨 ∣) → Im 𝒂 ⊆ Sg 𝑨 X → (f ̂ 𝑨) 𝒂 ∈ Sg 𝑨 X

\end{code}

For any subset `X` of the domain `∣ 𝑨 ∣` of an algebra `𝑨`, `Sg X` is a subuniverse.

\begin{code}

sgIsSub : {𝓤 𝓦 : Universe}{𝑨 : Algebra 𝓤 𝑆}{X : Pred ∣ 𝑨 ∣ 𝓦} → Sg 𝑨 X ∈ Subuniverses 𝑨
sgIsSub = app

\end{code}

By induction on the shape of elements of `Sg X`, we prove that `Sg X` is the smallest subuniverse of `𝑨` containing `X`.

\begin{code}

sgIsSmallest : {𝓤 𝓦 𝓡 : Universe}(𝑨 : Algebra 𝓤 𝑆){X : Pred ∣ 𝑨 ∣ 𝓦}(Y : Pred ∣ 𝑨 ∣ 𝓡)
 →             Y ∈ Subuniverses 𝑨  →  X ⊆ Y  →  Sg 𝑨 X ⊆ Y

sgIsSmallest _ _ _ X⊆Y (var v∈X) = X⊆Y v∈X
sgIsSmallest 𝑨 Y Y≤A X⊆Y (app f 𝒂 Ima⊆SgX) = fa∈Y
 where
  IH : Im 𝒂 ⊆ Y
  IH i = sgIsSmallest 𝑨 Y Y≤A X⊆Y (Ima⊆SgX i)

  fa∈Y : (f ̂ 𝑨) 𝒂 ∈ Y
  fa∈Y = Y≤A f 𝒂 IH

\end{code}

When the element of `Sg X` is constructed as `app f {a} ima⊆SgX`, we may assume (the induction hypothesis) that the arguments `a` belong to `Y`. Then the result of applying `f` to `a` must also belong to `Y`, since `Y` is a subuniverse.

---------------------------------

[← UALib.Subalgebras.Subuniverses](UALib.Subalgebras.Subuniverses.html)
<span style="float:right;">[UALib.Subalgebras.Properties →](UALib.Subalgebras.Properties.html)</span>

{% include UALib.Links.md %}
