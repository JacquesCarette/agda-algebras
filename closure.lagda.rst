.. FILE: closure.lagda.rst
.. AUTHOR: William DeMeo and Siva Somayyajula
.. DATE: 2 Jul 2020

===========================
Equational Logic in Agda
===========================

Closure operators
-----------------

Fix a signature 𝑆.

Let 𝒦 be a class of 𝑆-algebras. Define

  * H(𝒦) = homomorphic images of members of 𝒦;
  * S(𝒦) = algebras isomorphic to a subalgebra of a member of 𝒦;
  * P(𝒦) = algebras isomorphic to a direct product of members of 𝒦.

As a straight-forward verification confirms, H, S, and P are closure operators. A class 𝒦 of 𝑆-algebras is said to be *closed under the formation of homomorphic images* if H(𝒦) ⊆ 𝒦. Similarly, 𝒦 is *closed under the formation of subalgebras* (resp., *products*) provided S(𝒦) ⊆ 𝒦 (resp., P(𝒦) ⊆ 𝒦).

An algebra is a homomorphic image (resp., subalgebra; resp., product) of every algebra to which it is isomorphic. Thus, the class H(𝒦) (resp., S(𝒦); resp., P(𝒦)) is closed under isomorphism.

The operators H, S, and P can be composed with one another repeatedly, forming yet more closure operators. If C₁ and C₂ are closure operators on classes of structures, let us say that C₁ ≤ C₂ if for every class 𝒦 we have C₁(𝒦) ⊆ C₂(𝒦).

.. _lem 3.41:

.. proof:lemma:: Lem. 3.41 of :cite:`Bergman:2012`

   SH ≤ HS, PS ≤ SP.

   .. container:: toggle

      .. container:: header

         *Proof*.

      Let C ∈ SH(𝒦). Then C ≤ B for some B ∈ H(A), where A ∈ 𝒦.  Let θ be such that B ≅ A/θ.  Then C is isomorphic to a subalgebra, say, T, of A/θ.  By the correspondence theorem, there is a subalgebra S ≤ A such that S/θ = T.  Thus, C ∈ HS(A) ⊆ HS(𝒦), as desired.

      Let C ∈ PS(𝒦). Then C = Π Bᵢ for some Bᵢ ≤ Aᵢ ∈ 𝒦. Clearly, C = Π Bᵢ ≤ Π Aᵢ, so C ∈ SP(𝒦), as desired. ∎

---------------------------------------------

Varieties
-------------

A class 𝒦 of 𝑆-algebras is called a **variety** if it is closed under each of the closure operators H, S, and P introduced above; the corresponding closure operator is often denoted 𝕍. Thus, if 𝒦 is a class of similar algebras, then the **variety generated by** 𝒦 is denoted by 𝕍(𝒦) and defined to be the smallest class that contains 𝒦 and is closed under H, S, and P.

.. The class of all varieties of 𝑆-algebras is ordered by inclusion, and closed under arbitrary intersection; thus, the class of varieties is a complete lattice.

We would like to know how to construct 𝕍(𝒦) directly from 𝒦, but it's not immediately obvious how many times we would have to apply the operators H, S, P before the result stabilizes to form a variety---the **variety generated by** 𝒦.  Fortunately, Garrett Birkhoff proved that if we apply the operators in the correct order, then it suffices to apply each one only once.

.. proof:theorem:: Thm 3.43 of :cite:`Bergman:2012`

   𝕍 = HSP.

   .. container:: toggle

      .. container:: header

         *Proof*.

      Let 𝒦 be a class of algebras. To see that HSP(𝒦) is a variety, we use :numref:`Lemma %s <lem 3.41>` to compute H(HSP) = HSP, S(HSP) ≤ HS²P = HSP, P(HSP) ≤ HSP² = HSP. Thus HSP ≥ 𝕍.

      On the other hand, HSP(𝒦) ⊆ HSP(𝕍(𝒦)) = 𝕍(𝒦) so HSP ≤ 𝕍.

---------------------------------------------------

Equational classes
---------------------

In his treatment of Birhoff's HSP theorem, Cliff Bergman (at the start of Section 4.4 of his universal algebra textbook :cite:`Bergman:2012`) proclaims, "Now, finally, we can formalize the idea we have been using since the first page of this text."  He then proceeds to define **identities of terms** as follows (paraphrasing for notational consistency):

Let 𝑆 be a signature.  An **identity** or **equation** in 𝑆 is an ordered pair of terms, written 𝑝 ≈ 𝑞, from the term algebra 𝔉. If A is an 𝑆-algebra we say that A **satisfies** 𝑝 ≈ 𝑞 if 𝑝 ̇ A ≡ 𝑞 ̇ A.  In this  situation,  we  write A ⊧ 𝑝 ≈ 𝑞.

If 𝒦 is a class of 𝑆-algebras, we write 𝒦 ⊧ 𝑝 ≋ 𝑞 if, for every A ∈ 𝒦, A ⊧ 𝑝 ≈ 𝑞. Finally, if 𝓔 is a set of equations, we write 𝒦 ⊧ 𝓔 if every member of 𝒦 satisfies every member of 𝓔.

We formalize these notions in Agda in the ``closure`` module, which begins as follows.

::

  {-# OPTIONS --without-K --exact-split --safe #-}

  open import prelude

  open import basic using (Signature; Algebra; Π'; Op)

  open import subuniverses using (Subuniverses; SubunivAlg;
   hom-image-alg; _is-subalgebra-of_; Subalgebra)

  open import homomorphisms using (hom; is-homomorphism)

  open import terms using (Term; generator; node; _̇_; _̂_;
   interp-prod2; interp-prod; comm-hom-term')

  module closure {S : Signature 𝓞 𝓥} where

  _⊧_≈_ : {X : 𝓧 ̇ } → Algebra 𝓤 S
   →      Term{X = X} → Term → 𝓧 ⊔ 𝓤 ̇

  A ⊧ p ≈ q = (p ̇ A) ≡ (q ̇ A)

  _⊧_≋_ : {X : 𝓧 ̇ } → Pred (Algebra 𝓤 S) 𝓦
   →      Term{X = X} → Term → 𝓞 ⊔ 𝓥 ⊔ 𝓦 ⊔ 𝓧 ⊔ 𝓤 ⁺ ̇

  _⊧_≋_ 𝒦 p q = {A : Algebra _ S} → 𝒦 A → A ⊧ p ≈ q


---------------------------------------------

Identity preservation
----------------------

Identities are compatible with the formation of subalgebras, homomorphic images and products. More precisely,
for every class 𝒦 of structures, each of the classes S(𝒦), H(𝒦), P(𝒦), 𝕍(𝒦) satisfies the same set of identities as does 𝒦.

We formalize the notion of closure under the taking of homomorphic images in the `morphisms` module.  Here we will formalize closure under the taking of products and subuniverses, and prove that these closures preserve identities.

.. _obs 13 in agda:

Identities in products
~~~~~~~~~~~~~~~~~~~~~~~~

Let ℙ (𝒦) denote the class of algebras isomorphic to a direct product of members of 𝒦.

::

  P-closed : (𝓛𝒦 : (𝓤 : Universe) → Pred (Algebra 𝓤 S) (𝓤 ⁺ ))
   →      (𝓘 : Universe) (I : 𝓘 ̇ ) (𝒜 : I → Algebra 𝓘 S)
   →      (( i : I ) → 𝒜 i ∈ 𝓛𝒦 𝓘 ) → 𝓘 ⁺ ̇
  P-closed 𝓛𝒦 = λ 𝓘 I 𝒜 𝒜i∈𝓛𝒦 →  Π' 𝒜  ∈ (𝓛𝒦 𝓘)

  module _
    (gfe : global-dfunext)
    (𝒦 : Pred (Algebra 𝓤 S)(𝓞 ⊔ 𝓥 ⊔ ((𝓤 ⁺) ⁺))) { X : 𝓧 ̇ } where

    products-preserve-identities : (p q : Term{X = X})
          (I : 𝓤 ̇ ) (𝒜 : I → Algebra 𝓤 S)
     →    𝒦 ⊧ p ≋ q  →  ((i : I) → 𝒜 i ∈ 𝒦)
     →    Π' 𝒜 ⊧ p ≈ q
    products-preserve-identities p q I 𝒜 𝒦⊧p≋q all𝒜i∈𝒦 = γ
     where
      all𝒜⊧p≈q : ∀ i → (𝒜 i) ⊧ p ≈ q
      all𝒜⊧p≈q i = 𝒦⊧p≋q (all𝒜i∈𝒦 i)

      γ : (p ̇ Π' 𝒜) ≡ (q ̇ Π' 𝒜)
      γ = gfe λ a →
       (p ̇ Π' 𝒜) a
         ≡⟨ interp-prod gfe p 𝒜 a ⟩
       (λ i → ((p ̇ (𝒜 i)) (λ x → (a x) i)))
         ≡⟨ gfe (λ i → cong-app (all𝒜⊧p≈q i) (λ x → (a x) i)) ⟩
       (λ i → ((q ̇ (𝒜 i)) (λ x → (a x) i)))
         ≡⟨ (interp-prod gfe q 𝒜 a)⁻¹ ⟩
       (q ̇ Π' 𝒜) a
         ∎


Identities in subalgebras
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Let S(𝒦) denote the class of algebras isomorphic to a subalgebra of a member of 𝒦. We show that every term equation, 𝑝 ≈ 𝑞, that is satisfied by all A ∈ 𝒦 is also satisfied by all B ∈ S(𝒦).

::

  _is-subalgebra-of-class_ : {𝓤 : Universe} (B : Algebra 𝓤 S)
   →                 Pred (Algebra 𝓤 S)(𝓤 ⁺) → 𝓞 ⊔ 𝓥 ⊔ 𝓤 ⁺ ̇
  B is-subalgebra-of-class 𝒦 =
   Σ A ꞉ (Algebra _ S) , (A ∈ 𝒦) × (B is-subalgebra-of A)

  module _
   (𝒦 : Pred (Algebra 𝓤 S) ( 𝓤 ⁺ ))
   (𝒦' : Pred (Algebra 𝓤 S)(𝓞 ⊔ 𝓥 ⊔ ((𝓤 ⁺) ⁺))){X : 𝓧 ̇ }
   (𝓤★ : Univalence) where

   gfe : global-dfunext
   gfe = univalence-gives-global-dfunext 𝓤★

   SubalgebrasOfClass : Pred (Algebra 𝓤 S)(𝓤 ⁺) → 𝓞 ⊔ 𝓥 ⊔ 𝓤 ⁺ ̇
   SubalgebrasOfClass 𝒦 =
    Σ A ꞉ (Algebra _ S) , (A ∈ 𝒦) × Subalgebra{A = A} 𝓤★

   𝕊-closed : (𝓛𝒦 : (𝓤 : Universe) → Pred (Algebra 𝓤 S) (𝓤 ⁺))
    →      (𝓤 : Universe) → (B : Algebra 𝓤 S) → 𝓞 ⊔ 𝓥 ⊔ 𝓤 ⁺ ̇
   𝕊-closed 𝓛𝒦 =
    λ 𝓤 B → (B is-subalgebra-of-class (𝓛𝒦 𝓤)) → (B ∈ 𝓛𝒦 𝓤)

   subalgebras-preserve-identities : (p q : Term{X = X})
    →  (𝒦 ⊧ p ≋ q) → (SAK : SubalgebrasOfClass 𝒦)
    →  (pr₁ ∥ (pr₂ SAK) ∥) ⊧ p ≈ q
   subalgebras-preserve-identities p q 𝒦⊧p≋q SAK = γ
    where

     A : Algebra 𝓤 S
     A = ∣ SAK ∣

     A∈𝒦 : A ∈ 𝒦
     A∈𝒦 = ∣ pr₂ SAK ∣

     A⊧p≈q : A ⊧ p ≈ q
     A⊧p≈q = 𝒦⊧p≋q A∈𝒦

     subalg : Subalgebra{A = A} 𝓤★
     subalg = ∥ pr₂ SAK ∥

     B : Algebra 𝓤 S
     B = pr₁ subalg

     h : ∣ B ∣ → ∣ A ∣
     h = ∣ pr₂ subalg ∣

     h-emb : is-embedding h
     h-emb = pr₁ ∥ pr₂ subalg ∥

     h-hom : is-homomorphism B A h
     h-hom = pr₂ ∥ pr₂ subalg ∥

     ξ : (𝒃 : X → ∣ B ∣ ) → h ((p ̇ B) 𝒃) ≡ h ((q ̇ B) 𝒃)
     ξ 𝒃 =
      h ((p ̇ B) 𝒃)  ≡⟨ comm-hom-term' gfe B A (h , h-hom) p 𝒃 ⟩
      (p ̇ A)(h ∘ 𝒃) ≡⟨ intensionality A⊧p≈q (h ∘ 𝒃) ⟩
      (q ̇ A)(h ∘ 𝒃) ≡⟨ (comm-hom-term' gfe B A (h , h-hom) q 𝒃)⁻¹ ⟩
      h ((q ̇ B) 𝒃)  ∎

     hlc : {b b' : domain h} → h b ≡ h b' → b ≡ b'
     hlc hb≡hb' = (embeddings-are-lc h h-emb) hb≡hb'

     γ : B ⊧ p ≈ q
     γ = gfe λ 𝒃 → hlc (ξ 𝒃)


Closure under H, S, P
----------------------

::

  data PClo (𝒦 : Pred (Algebra 𝓤 S) 𝓣) : Pred (Algebra 𝓤 S)(𝓞 ⊔ 𝓥 ⊔ 𝓣 ⊔ 𝓤 ⁺ ) where
   pbase : {A : Algebra 𝓤 S} → A ∈ 𝒦 → A ∈ PClo 𝒦
   prod : {I : 𝓤 ̇ }{𝒜 : I → Algebra _ S}
    →     (∀ i → 𝒜 i ∈ PClo 𝒦)
    →     Π' 𝒜 ∈ PClo 𝒦

  data SClo (𝒦 : Pred (Algebra 𝓤 S) 𝓣) : Pred (Algebra 𝓤 S)(𝓞 ⊔ 𝓥 ⊔ 𝓣 ⊔ 𝓤 ⁺ ) where
   sbase : {A : Algebra _ S} → A ∈ 𝒦 → A ∈ SClo 𝒦
   --sub : {A B : Algebra _ S} → A ∈ SClo 𝒦 → B is-subalgebra-of A → B ∈ SClo 𝒦
   --sub : {A : Algebra _ S} → A ∈ SClo 𝒦 → B is-subalgebra-of A → B ∈ SClo 𝒦
   sub : {A : Algebra _ S} {B : Pred ∣ A ∣ 𝓤 }
         {𝐹 : (𝓸 : ∣ S ∣) → Op (∥ S ∥ 𝓸) (Σ B)}
         (B∈SubA : B ∈ Subuniverses A)
    →    A ∈ SClo 𝒦
    →    SubunivAlg{A = A}{B = B}{𝐹 = 𝐹} B∈SubA ∈ SClo 𝒦

  data HClo (𝒦 : Pred (Algebra 𝓤 S) 𝓣) : Pred (Algebra 𝓤 S)(𝓞 ⊔ 𝓥 ⊔ 𝓣 ⊔ 𝓤 ⁺ ) where
   hbase : {A : Algebra 𝓤 S} → A ∈ 𝒦 → A ∈ HClo 𝒦
   hhom : {A B : Algebra 𝓤 S}{f : hom A B}
    →     A ∈ HClo 𝒦
    →     hom-image-alg {A = A}{B = B} f ∈ HClo 𝒦

  data VClo (𝒦 : Pred (Algebra 𝓤 S) 𝓣) : Pred (Algebra 𝓤 S)(𝓞 ⊔ 𝓥 ⊔ 𝓣 ⊔ 𝓤 ⁺ ) where
   vbase : {A : Algebra 𝓤 S} → A ∈ 𝒦 → A ∈ VClo 𝒦
   vprod : {I : 𝓤 ̇ }{𝒜 : I → Algebra _ S} → (∀ i → 𝒜 i ∈ VClo 𝒦) → Π' 𝒜 ∈ VClo 𝒦
   vsub : ∀{A : Algebra _ S}{B : Algebra _ S} → A ∈ VClo 𝒦 → B is-subalgebra-of A → B ∈ VClo 𝒦
   vhom : {A B : Algebra 𝓤 S}{f : hom A B}
    →     A ∈ VClo 𝒦 → hom-image-alg {A = A}{B = B} f ∈ VClo 𝒦

---------------------------------------------

Alternative formulations
----------------------------

::

  module _
   (𝒦 : Pred (Algebra 𝓤 S) 𝓣)
   (gfe : global-dfunext)
   (dfe : dfunext 𝓤 𝓤)
   {X : 𝓤 ̇ } where

   _⊧'_≋_ : Pred (Algebra 𝓤 S) 𝓦 → Term {X = X} → Term → 𝓞 ⊔ 𝓥 ⊔ 𝓦 ⊔ 𝓤 ⁺ ̇
   _⊧'_≋_ = _⊧_≋_ {X = X}

   pclo-id1 : ∀ {p q} → (𝒦 ⊧ p ≋ q) → (PClo 𝒦 ⊧ p ≋ q)
   pclo-id1 {p} {q} α (pbase x) = α x
   pclo-id1 {p} {q} α (prod{I}{𝒜} 𝒜-P𝒦 ) = γ
    where
     IH : (i : I)  → (p ̇ 𝒜 i) ≡ (q ̇ 𝒜 i)
     IH = λ i → pclo-id1{p}{q} α  ( 𝒜-P𝒦  i )
     γ : p ̇ (Π' 𝒜)  ≡ q ̇ (Π' 𝒜)
     γ =
      (p ̇ (Π' 𝒜) )
        ≡⟨ interp-prod2 gfe p 𝒜 ⟩
      (λ (args : X → ∣ Π' 𝒜 ∣) → (λ i → (p ̇ 𝒜 i)(λ x → (args x) i)))
        ≡⟨ dfe (λ args → (ap (λ - → (λ i → (- i)(λ x → args x i))) (dfe IH))) ⟩
      (λ (args : X → ∣ Π' 𝒜 ∣) → (λ i → (q ̇ 𝒜 i)(λ x → (args x) i)))
        ≡⟨ (interp-prod2 gfe q 𝒜)⁻¹ ⟩
      (q ̇ (Π' 𝒜))
        ∎

   pclo-id2 : ∀{p q} → ((PClo 𝒦) ⊧' p ≋ q ) → (𝒦 ⊧ p ≋ q)
   pclo-id2 p A∈𝒦 = p (pbase A∈𝒦)

   sclo-id1 : ∀{p q} → (𝒦 ⊧' p ≋ q) → (SClo 𝒦 ⊧ p ≋ q)
   sclo-id1 {p} {q} 𝒦⊧p≋q (sbase A∈𝒦) = 𝒦⊧p≋q A∈𝒦
   sclo-id1 {p} {q} 𝒦⊧p≋q (sub {A = A}{B = B}{𝐹 = 𝐹} B∈SubA A∈SClo𝒦) = γ
    where
     IH : p ̇ A ≡ q ̇ A
     IH = sclo-id1{p}{q} 𝒦⊧p≋q A∈SClo𝒦

     B : Algebra 𝓤 S
     B = SubunivAlg{A = A}{B = B}{𝐹 = 𝐹} B∈SubA
     -- We need to do this so that both A and Σ B , 𝐹 can be classified by the same predicate SClo.
     -- tB≡tA : ∀ 𝒕 → ( 𝒃 : X → Σ B ) → ( 𝒕 ̇ (Σ B , 𝐹) )( λ x →  𝒃 x ) ≡ (𝒕 ̇ A) (λ x →  ∣ 𝒃 x ∣ )
     -- tB≡tA 𝒕 = ?
      -- mem :   {B : Pred ∣ A ∣ 𝓤}  { 𝐹 : ( 𝓸 : ∣ S ∣ ) → Op ( ∥ S ∥ 𝓸 ) (Σ B) }
      --   →    ( ( 𝓸 : ∣ S ∣ ) ( a : ∥ S ∥ 𝓸 → Σ B )  →  ∣ 𝐹 𝓸 a ∣ ≡ ∥ A ∥ 𝓸 (λ i → ∣ a i ∣ ) )
      --   →    A is-supalgebra-of (Σ B , 𝐹)
     uni2alg : B is-subalgebra-of A
     uni2alg = ?

     γ : p ̇ B ≡ q ̇ B
     γ = let sts = uni2alg in
      gfe λ 𝒃 →
       (p ̇ B) 𝒃 ≡⟨ ? ⟩  -- we need an elimination rule here (see is-subalg-elim in UF-Subuniverse.agda)
         -- (p ̇ uni2alg) 𝒃 ≡⟨ IH ⟩
         -- (q ̇ uni2alg) 𝒃 ≡⟨ ? ⟩
       (q ̇ B) 𝒃  ∎

   sclo-id2 : ∀ {p q} → (SClo 𝒦 ⊧' p ≋ q) → (𝒦 ⊧ p ≋ q)
   sclo-id2 p A∈𝒦 = p (sbase A∈𝒦)

   hclo-id1 : ∀{p q} → (𝒦 ⊧ p ≋ q) → (HClo 𝒦 ⊧ p ≋ q)
   hclo-id1 {p}{q} 𝒦⊧p≋q (hbase A∈𝒦) = 𝒦⊧p≋q A∈𝒦
   hclo-id1 {p}{q} 𝒦⊧p≋q (hhom{A}{B}{f} A∈HClo𝒦) = γ
    where
     A⊧p≈q : A ⊧ p ≈ q
     A⊧p≈q = (hclo-id1{p}{q} 𝒦⊧p≋q ) A∈HClo𝒦

     IH : (p ̇ A) ≡ (q ̇ A)
     IH = A⊧p≈q

     HIA = hom-image-alg{A = A}{B = B} f

     preim : (𝒃 : X → Σ (Image_∋_ ∣ f ∣))(x : X) → ∣ A ∣
     preim = λ 𝒃 x → (Inv ∣ f ∣ (∣ 𝒃 x ∣)(∥ 𝒃 x ∥))

     hom-image-term-interpretation hiti : (𝒃 : X → ∣ HIA ∣)(p : Term)
      → (p ̇ HIA ) 𝒃 ≡ ∣ f ∣ ((p ̇ A)( λ i → preim 𝒃 i )) , im ((p ̇ A)(λ i → preim 𝒃 i))

     hom-image-term-interpretation 𝒃 (generator x) =
      let iiif = ( InvIsInv ∣ f ∣ ∣ 𝒃 x ∣ ∥ 𝒃 x ∥ )⁻¹ in
       𝒃 x ≡⟨ ? ⟩ ∣ f ∣ (preim 𝒃 x) , im (preim 𝒃 x) ∎

     hom-image-term-interpretation 𝒃 (node 𝓸 𝒕) =  ap (λ - → (𝓸 ̂ HIA) -) (gfe λ x → φIH x)
      where
       φIH : (x : ∥ S ∥ 𝓸)
        → ( 𝒕 x ̇ HIA ) 𝒃  ≡ ∣ f ∣ ( ( 𝒕 x ̇ A ) (preim 𝒃) ) , im ((𝒕 x ̇ A) (preim 𝒃 ) )
       φIH x = hom-image-term-interpretation 𝒃 (𝒕 x)

     hiti = hom-image-term-interpretation  -- alias

     γ : (p ̇ HIA) ≡ (q ̇ HIA)
     γ = (p ̇ HIA)
               ≡⟨ refl _ ⟩
           ( λ ( 𝒃 : X → ∣ HIA ∣ ) → (p ̇ HIA) ( λ x → (𝒃 x) ) )
               ≡⟨ gfe (λ x → hiti x p) ⟩
           ( λ 𝒃 → ∣ f ∣ ( (p ̇ A) ( λ x → preim 𝒃 x ) ) , im ( (p ̇ A) ( λ x → preim 𝒃 x ) ) )
               ≡⟨ ap (λ - → λ 𝒃 → ∣ f ∣ (- (λ x → preim 𝒃 x) )  , im (-  (λ x → preim 𝒃 x) )) IH ⟩
           ( λ 𝒃 → ∣ f ∣ ( (q ̇ A) ( λ x → preim 𝒃 x ) ) , im ( (q ̇ A) ( λ x → preim 𝒃 x ) ) )
               ≡⟨ ( gfe (λ x → hiti x q) )⁻¹ ⟩
           ( λ 𝒃 → (q ̇ HIA) ( λ x → (𝒃 x) ) )
               ≡⟨ refl _ ⟩
           (q ̇ HIA)    ∎

   hclo-id2 : ∀ {p q} → (HClo 𝒦 ⊧' p ≋ q) → (𝒦 ⊧ p ≋ q)
   hclo-id2 p A∈𝒦 = p (hbase A∈𝒦)

   vclo-id1 : ∀ {p q} → (𝒦 ⊧' p ≋ q) → (VClo 𝒦 ⊧ p ≋ q)
   vclo-id1 {p} {q} α (vbase A∈𝒦) = α A∈𝒦
   vclo-id1 {p} {q} α (vprod{I = I}{𝒜 = 𝒜} allAi∈VClo𝒦) = γ
     where
      IH : (i : I) → 𝒜 i ⊧ p ≈ q
      IH i = vclo-id1{p}{q} α (allAi∈VClo𝒦 i)

      γ : p ̇ (Π' 𝒜)  ≡ q ̇ (Π' 𝒜)
      γ =
       (p ̇ (Π' 𝒜))
         ≡⟨ interp-prod2 gfe p 𝒜 ⟩
       (λ (args : X → ∣ Π' 𝒜 ∣) → (λ i → (p ̇ 𝒜 i)(λ x → (args x) i)))
         ≡⟨ dfe (λ args → (ap (λ - → (λ i → (- i)(λ x → args x i))) (dfe IH))) ⟩
       (λ (args : X → ∣ Π' 𝒜 ∣) → (λ i → (q ̇ 𝒜 i)(λ x → (args x) i)))
         ≡⟨ (interp-prod2 gfe q 𝒜)⁻¹ ⟩
       (q ̇ (Π' 𝒜))
         ∎

   --vsub : ∀ {A : Algebra _ S} {B : Algebra _ S} → A ∈ VClo 𝒦 → B is-subalgebra-of A → B ∈ VClo 𝒦
   vclo-id1 {p} {q} α ( vsub {A = A}{B = B} A∈VClo𝒦 B≤A ) = γ
     where
      γ : B ⊧ p ≈ q
      γ = ?

   --vhom : {A B : Algebra 𝓤 S} {f : Hom A B} → A ∈ VClo 𝒦 →  hom-image-alg {A = A}{B = B} f ∈ VClo 𝒦
   vclo-id1 {p} {q} α ( vhom{A = A}{B = B}{f = f} A∈VClo𝒦 ) = γ
     where
      γ : hom-image-alg{A = A}{B = B} f ⊧ p ≈ q
      γ = {!!}

   vclo-id2 : ∀ {p q} → (VClo 𝒦 ⊧' p ≋ q) → (𝒦 ⊧ p ≋ q)
   vclo-id2 p A∈𝒦 = p (vbase A∈𝒦)

